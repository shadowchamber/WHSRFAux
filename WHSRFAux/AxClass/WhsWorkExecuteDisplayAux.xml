<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WhsWorkExecuteDisplayAux</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// WhsWorkExecuteDisplay auxiliary class.
/// </summary>
public class WhsWorkExecuteDisplayAux extends WhsWorkExecuteDisplay
{
    #WHSRF
    #WHSRFAux

    protected container originalContainer;
    protected List messageQueue;
    protected container switchProcessContainer;
    protected ClassName serviceClassName;
    protected MethodName serviceMethodName;
    protected ClassName modelClassName;
    protected str doneText;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>displayForm</Name>
				<Source><![CDATA[
    public container displayForm(
        container   _con,
        str         _extraText = '')
    {
        SysDictClass dictClass = new SysDictClass(classIdGet(this));
        WHSRFDataServiceAuxAttribute serviceAttribute = dictClass.getAttribute(classStr(WHSRFDataServiceAuxAttribute)) as WHSRFDataServiceAuxAttribute;

        serviceClassName = serviceAttribute.serviceClassName();
        serviceMethodName = serviceAttribute.serviceMethodName();

        SysDictClass dictClassModel = new SysDictClass(className2Id(serviceAttribute.serviceClassName()));

        SysDictMethod dictMethod = dictClassModel.getMethod(serviceMethodName);

        if (dictMethod.parameterCnt() != 1)
        {
            throw Error("@WHSRFAux:ERR001");
        }

        modelClassName = dictMethod.parameterTypeName(1);

        originalContainer = _con;

        messageQueue = new List(Types::Container);

        pass = WHSRFPassthrough::create(conPeek(_con, #PassthroughInfo));
        
        Object model = this.getModelAux();

        this.generateEventMessagesAux(model);

        model = this.processMessagesAux(model);

        return this.viewAux(model, _con);
    }

]]></Source>
			</Method>
			<Method>
				<Name>processMessagesAux</Name>
				<Source><![CDATA[
    /// <summary>
    /// Process MVU messages.
    /// </summary>
    /// <param name = "_model">model object (state).</param>
    /// <returns>model object (modified).</returns>
    protected Object processMessagesAux(Object _model)
    {
        Object model = _model;

        while (messageQueue.elements() > 0)
        {
            List messagesToProcess = new List(Types::Container);

            messagesToProcess.appendList(messageQueue);

            messageQueue = new List(Types::Container);

            ListEnumerator listEnumerator = messagesToProcess.getEnumerator();

            while (listEnumerator.moveNext())
            {
                str message;
                anytype data;

                [message, data] = listEnumerator.current();

                model = this.updateAux(message, model, data);
            }
        }

        return model;
    }

]]></Source>
			</Method>
			<Method>
				<Name>switchToProcessAux</Name>
				<Source><![CDATA[
    /// <summary>
    /// Mark to switch to another WHS process.
    /// </summary>
    /// <param name = "_model">nodel object.</param>
    /// <param name = "_con">modified container for switching.</param>
    /// <returns>model object.</returns>
    protected Object switchToProcessAux(Object _model, container _con)
    {
        Object model = _model;

        switchProcessContainer = _con;

        return model;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateAux</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates model (MVU).
    /// </summary>
    /// <param name = "_message">message name string.</param>
    /// <param name = "_model">model object.</param>
    /// <param name = "_data">message data for model updating.</param>
    /// <returns>model object.</returns>
    protected Object updateAux(str _message, Object _model, anytype _data)
    {
        Object model = _model;

        Debug::assert(_message != '');

        if (!objectHasMethod(this, _message))
        {
            throw error(strFmt("@WHSRFAux:ERR002", _message));
        }

        SysDictClass dictClass = new SysDictClass(classIdGet(this));

        SetEnumerator se =  dictClass.methods().getEnumerator();

        while (se.moveNext())
        {
            SysDictMethod method = se.current() as SysDictMethod;

            if (method.name() != _message)
            {
                continue;
            }

            if (method.parameterCnt() == 2)
            {
                model = dictClass.callObject(_message, this, _model, _data);
            }
            else if (method.parameterCnt() == 1)
            {
                model = dictClass.callObject(_message, this, _model);
            }
            else
            {
                Debug::assert(false);

                throw Error("@WHSRFAux:ERR003");
            }

            break;
        }

        return model;
    }

]]></Source>
			</Method>
			<Method>
				<Name>readFieldValueViewAux</Name>
				<Source><![CDATA[
    /// <summary>
    /// Reads field value from form.
    /// </summary>
    /// <param name = "_con">WHSRF container.</param>
    /// <param name = "_model">model object.</param>
    /// <returns>WHSRF form container.</returns>
    protected container readFieldValueViewAux(container _con, Object _model)
    {
        container ret = _con;

        SysDictClass dictClass = new SysDictClass(classIdGet(_model));

        WHSRFModelFieldAuxAttribute fieldAttribute;
        SysDictMethod fieldMethodInfo = this.getCurrentFieldMethodInfoAux(_model);

        fieldAttribute = fieldMethodInfo.getAttribute(classStr(WHSRFModelFieldAuxAttribute)) as WHSRFModelFieldAuxAttribute;

        if (fieldAttribute == null
            || fieldMethodInfo == null)
        {
            return ret;
        }

        str typeName = fieldMethodInfo.returnTypeName();
        
        ExtendedTypeId extTypeId = extendedTypeName2Id(typeName);

        WHSRFModelAuxAttribute modelAttribute = dictClass.getAttribute(classStr(WHSRFModelAuxAttribute)) as WHSRFModelAuxAttribute;

        anytype value = dictClass.callObject(fieldMethodInfo.name(), _model);

        SysDictType dictType = new SysDictType(extTypeId);

        str labelFieldName = fieldMethodInfo.name() + 'Label';
        str textFieldName = fieldMethodInfo.name() + 'Text';
 
        ret += [this.buildControl(#RFLabel, labelFieldName, modelAttribute.title(), 1, '', #WHSRFUndefinedDataType, '', 0)];
        ret += [this.buildControl(#RFText, textFieldName, dictType.label(), 1, strFmt('%1', value), extTypeId, '', 0)];
        ret += [this.buildControl(#RFButton, #RFOK, "@SYS5473", 1, '', #WHSRFUndefinedDataType, '', 1)];
        ret += [this.buildControl(#RFButton, #RFCancel, "@SYS50163", 1, '', #WHSRFUndefinedDataType, '', 0)];
 
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initAux</Name>
				<Source><![CDATA[
    /// <summary>
    /// Default init message handler.
    /// </summary>
    /// <param name = "_model">model object.</param>
    /// <returns>model object.</returns>
    protected Object initAux(Object _model)
    {
        Object ret = _model;

        SysDictClass dictClass = new SysDictClass(classIdGet(_model));

        ret = dictClass.makeObject();

        this.setPageNameAux(#ReadFieldValue);

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>doneViewAux</Name>
				<Source><![CDATA[
    /// <summary>
    /// Done view.
    /// </summary>
    /// <param name = "_con">WHSRF container.</param>
    /// <param name = "_model">model object.</param>
    /// <returns>WHSRF form container.</returns>
    protected container doneViewAux(container _con, Object _model)
    {
        container ret = _con;

        SysDictClass dictClass = new SysDictClass(classIdGet(_model));
        WHSRFModelAuxAttribute modelAttribute = dictClass.getAttribute(classStr(WHSRFModelAuxAttribute)) as WHSRFModelAuxAttribute;
 
        ret += [this.buildControl(#RFLabel, 'TitleLabel', modelAttribute.title(), 1, '', #WHSRFUndefinedDataType, '', 0)];

        SysInfologEnumerator sysInfologEnumerator = SysInfologEnumerator::newData(infolog.infologData());
        int i = 1;

        while (sysInfologEnumerator.moveNext())
        {
            SysInfologMessageStruct infoMessageStruct = SysInfologMessageStruct::construct(sysInfologEnumerator.currentMessage());

            ret += [this.buildControl(#RFLabel, strFmt('DoneLabel %1', i), infoMessageStruct.message(), 1, '', #WHSRFUndefinedDataType, '', 0)];

            i++;
        }

        ret += [this.buildControl(#RFButton, #RFOK, "@SYS5473", 1, '', #WHSRFUndefinedDataType, '', 1)];
        ret += [this.buildControl(#RFButton, #RFCancel, "@SYS50163", 1, '', #WHSRFUndefinedDataType, '', 0)];
 
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>invokeServiceMethodAux</Name>
				<Source><![CDATA[
    /// <summary>
    /// Invokes service method assigned by WHSRFDataServiceAux attirbute.
    /// </summary>
    /// <param name = "_model">model object.</param>
    /// <returns>model object.</returns>
    protected Object invokeServiceMethodAux(Object _model)
    {
        SysDictClass dictClass = new SysDictClass(className2Id(serviceClassName));

        if (!dictClass.hasObjectMethod(serviceMethodName))
        {
            throw Error(strFmt("@WHSRFAux:ERR004", serviceMethodName));
        }

        Object service = dictClass.makeObject();

        dictClass.callObject(serviceMethodName, service, _model);

        this.setPageNameAux('done');

        step = 0;

        return _model;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateModelByFieldAux</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates model by field value.
    /// </summary>
    /// <param name = "_model">model object.</param>
    /// <param name = "_value">value (anytype)</param>
    /// <returns>model object.</returns>
    protected Object updateModelByFieldAux(Object _model, anytype _value)
    {
        SysDictClass dictClass = new SysDictClass(classIdGet(_model));

        WHSRFModelFieldAuxAttribute fieldAttribute;
        SysDictMethod fieldMethodInfo = this.getCurrentFieldMethodInfoAux(_model);

        if (fieldMethodInfo == null)
        {
            return _model;
        }

        fieldAttribute = fieldMethodInfo.getAttribute(classStr(WHSRFModelFieldAuxAttribute)) as WHSRFModelFieldAuxAttribute;

        if (fieldAttribute == null)
        {
            return _model;
        }

        dictClass.callObject(fieldMethodInfo.name(), _model, _value);

        this.setPageNameAux(#ReadFieldValue);

        if (subStr(fieldMethodInfo.name(), 1, strLen(#Parm)) == #Parm)
        {
            str validateMethodName = 'validate' + subStr(fieldMethodInfo.name(), 1 + strLen(#Parm), strLen(fieldMethodInfo.name()) - strLen(#Parm));

            if (dictClass.hasObjectMethod(validateMethodName))
            {
                boolean valid = dictClass.callObject(validateMethodName, _model);

                if (!valid)
                {
                    throw Error('Value is not correct.');
                }
            }
        }

        boolean visible = false;

        while (!visible)
        {
            step++;

            fieldMethodInfo = this.getCurrentFieldMethodInfoAux(_model); // for checking if finished
        
            if (fieldMethodInfo == null)
            {
                this.dispatchAux(methodStr(WhsWorkExecuteDisplayAux, invokeServiceMethodAux), _value);

                return _model;
            }

            if (subStr(fieldMethodInfo.name(), 1, strLen(#Parm)) == #Parm)
            {
                str visibleMethodName = 'visible' + subStr(fieldMethodInfo.name(), 1 + strLen(#Parm), strLen(fieldMethodInfo.name()) - strLen(#Parm));

                if (dictClass.hasObjectMethod(visibleMethodName))
                {
                    visible = dictClass.callObject(visibleMethodName, _model, pass.lookupStr(#MenuItem));
                }
                else
                {
                    visible = true;
                }
            }
        }


        return _model;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCurrentFieldMethodInfoAux</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets current field SysDictMethod object.
    /// </summary>
    /// <param name = "_model">model object.</param>
    /// <returns>SysDictMethod object.</returns>
    protected SysDictMethod getCurrentFieldMethodInfoAux(Object _model)
    {
        SysDictClass dictClass = new SysDictClass(classIdGet(_model));

        Set methods = dictClass.methods();
        SetEnumerator methodsEnumerator = methods.getEnumerator();

        SysDictMethod fieldMethodInfo;

        while (methodsEnumerator.moveNext())
        {
            SysDictMethod methodInfo = methodsEnumerator.current();

            WHSRFModelFieldAuxAttribute attribute = methodInfo.getAttribute(classStr(WHSRFModelFieldAuxAttribute)) as WHSRFModelFieldAuxAttribute;

            if (attribute == null)
            {
                continue;
            }

            if (attribute.step() != step)
            {
                continue;
            }

            fieldMethodInfo = methodInfo;

            break;
        }

        return fieldMethodInfo;
    }

]]></Source>
			</Method>
			<Method>
				<Name>generateEventMessagesAux</Name>
				<Source><![CDATA[
    /// <summary>
    /// Generates standard event mesages.
    /// </summary>
    /// <param name = "_model">model object.</param>
    protected void generateEventMessagesAux(Object _model)
    {
        if (step == 0)
        {
            SysDictClass dictClass = new SysDictClass(classIdGet(this));

            if(dictClass.hasObjectMethod(#Init))
            {
                this.dispatchAux(#Init, null);
            }
            else
            {
                this.dispatchAux(methodStr(WhsWorkExecuteDisplayAux, initAux), null);
            }

            step = 1;
        }
        else
        {
            WHSRFModelFieldAuxAttribute fieldAttribute;
            SysDictMethod fieldMethodInfo = this.getCurrentFieldMethodInfoAux(_model);

            fieldAttribute = fieldMethodInfo.getAttribute(classStr(WHSRFModelFieldAuxAttribute)) as WHSRFModelFieldAuxAttribute;

            if (fieldAttribute == null
                || fieldMethodInfo == null)
            {
                return;
            }

            str textFieldName = fieldMethodInfo.name() + 'Text';

            str	value = this.getControlDataFromContainer(this.parmOriginalContainerAux(), textFieldName);

            this.dispatchAux(methodStr(WhsWorkExecuteDisplayAux, updateModelByFieldAux), value);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>dispatchAux</Name>
				<Source><![CDATA[
    /// <summary>
    /// Add message to the queue for processing.
    /// </summary>
    /// <param name = "_message">message name string.</param>
    /// <param name = "_data">anytype message data.</param>
    protected void dispatchAux(str _message, anytype _data = null)
    {
        messageQueue.addEnd([_message, _data]);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmOriginalContainerAux</Name>
				<Source><![CDATA[
    /// <summary>
    /// Standard parm method.
    /// </summary>
    /// <param name = "_originalContainer">Value to set.</param>
    /// <returns>Return value.</returns>
    protected container parmOriginalContainerAux(container _originalContainer = originalContainer)
    {
        originalContainer = _originalContainer;
    
        return originalContainer;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setPageNameAux</Name>
				<Source><![CDATA[
    /// <summary>
    /// Puts page name string to pass.
    /// </summary>
    /// <param name = "_pageName">page name string.</param>
    protected void setPageNameAux(str _pageName)
    {
        if (pass.exists(#PageName))
        {
            pass.remove(#PageName);
        }

        pass.insert(#PageName, _pageName);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPageNameAux</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets page name string from pass.
    /// </summary>
    /// <returns>page name string.</returns>
    protected str getPageNameAux()
    {
        return pass.exists(#PageName) ? pass.lookupStr(#PageName) : '';
    }

]]></Source>
			</Method>
			<Method>
				<Name>setModelAux</Name>
				<Source><![CDATA[
    /// <summary>
    /// Puts serialized model object to pass.
    /// </summary>
    /// <param name = "_model">model object.</param>
    protected void setModelAux(Object _model)
    {
        str packedModel = FormJsonSerializer::serializeClass(_model);

        if (pass.exists(#Model))
        {
            pass.remove(#Model);
        }

        pass.insert(#Model, packedModel);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getModelAux</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets deserialized model object from pass.
    /// </summary>
    /// <returns>model object.</returns>
    protected Object getModelAux()
    {
        Object model;

        str packedModel = pass.exists(#Model) ? pass.lookupStr(#Model) : '';
        
        if (packedModel == '')
        {
            SysDictClass dictClassModel = new SysDictClass(className2Id(modelClassName));

            model = dictClassModel.makeObject();
        }
        else
        {
            model = FormJsonSerializer::deserializeObject(className2Id(modelClassName), packedModel);
        }

        return model;
    }

]]></Source>
			</Method>
			<Method>
				<Name>viewAux</Name>
				<Source><![CDATA[
    /// <summary>
    /// Main view method (MVU) which calls view method for current page by pageName + View.
    /// </summary>
    /// <param name = "_model">model object.</param>
    /// <param name = "_con">WHSRF container.</param>
    /// <returns>WHSRF form container.</returns>
    protected container viewAux(Object _model, container _con)
    {
        container res = connull();
        container con = _con;

        if (this.hasError(_con))
        {
            con = conDel(con, #ControlsStart, 1);
        }

        this.setModelAux(_model);

        str pageName = this.getPageNameAux();

        Debug::assert(pageName != '');

        SysDictClass dictClass = new SysDictClass(classIdGet(this));

        if (dictClass.hasObjectMethod(pageName + #ViewMethodPostfix))
        {
            res = dictClass.callObject(pageName + #ViewMethodPostfix, this, res, _model);
        }
        else if (dictClass.hasObjectMethod(pageName + #ViewMethodPostfix + #Aux))
        {
            res = dictClass.callObject(pageName + #ViewMethodPostfix + #Aux, this, res, _model);
        }

        res = this.updateModeStepPass(res, mode, step, pass);

        return res;
    }

]]></Source>
			</Method>
			<Method>
				<Name>cancelClicked</Name>
				<Source><![CDATA[
    /// <summary>
    /// Handles cancel button click.
    /// </summary>
    protected void cancelClicked()
    {
        mode = WHSWorkExecuteMode::Menu;
        step = 0;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>